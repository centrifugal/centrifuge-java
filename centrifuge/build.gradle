apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'java-library'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

group = "io.github.centrifugal"
archivesBaseName = "centrifuge-java"
version = "0.0.3"

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'com.google.protobuf:protobuf-java:3.7.0'
    implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.7.0'
    implementation 'com.google.code.gson:gson:2.8.6'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'

    testImplementation 'junit:junit:4.12'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: findProperty('ossrhUsername'), password: findProperty('ossrhPassword'))
            }

            pom.project {
                name 'Centrifuge Java'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Android and General Java client for Centrifugo server and Centrifuge library'
                url 'https://github.com/centrifugal/centrifuge-java'

                scm {
                    connection 'scm:git:git://github.com/centrifugal/centrifuge-java.git'
                    developerConnection 'scm:git:ssh://github.com:centrifugal/centrifuge-java.git'
                    url 'http://github.com/centrifugal/centrifuge-java'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'FZambia'
                        name 'Alexander Emelin'
                        email 'frvzmb@gmail.com'
                    }
                }
            }
        }
    }
}
