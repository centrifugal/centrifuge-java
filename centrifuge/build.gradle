import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

plugins {
    id "java-library"
    id "maven-publish"
    id "signing"
    id "com.google.protobuf" version "0.8.19"
}

apply plugin: "com.vanniktech.maven.publish.base"

// See https://github.com/vanniktech/gradle-maven-publish-plugin#base-plugin
mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.9.3'
    implementation 'com.google.protobuf:protobuf-javalite:3.20.1'
    implementation 'net.sourceforge.streamsupport:streamsupport-cfuture:1.7.0'

    testImplementation 'junit:junit:4.13.2'
}

java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

sourceSets {
    main {
        java {
            String baseDir = protobuf.getProperty("generatedFilesBaseDir").toString()
            srcDirs "$baseDir/main/java"
        }
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.20.1"
    }

    generateProtoTasks {
        all().configureEach {
            builtins {
                java {
                    option("lite")
                }
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'centrifuge-java'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Centrifuge Java'
                description = 'Android and General Java client for Centrifugo server and Centrifuge library'
                url = 'https://github.com/centrifugal/centrifuge-java'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id = 'FZambia'
                        name = 'Alexander Emelin'
                        email = 'frvzmb@gmail.com'
                    }
                }

                scm {
                    connection = 'scm:git:git://github.com/centrifugal/centrifuge-java.git'
                    developerConnection = 'scm:git:ssh://github.com:centrifugal/centrifuge-java.git'
                    url = 'http://github.com/centrifugal/centrifuge-java'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
            credentials {
                username = findProperty('ossrhUsername')
                password = findProperty('ossrhPassword')
            }
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
